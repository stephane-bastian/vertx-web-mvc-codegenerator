grammar com.thesoftwarefactory.vertx.web.mvc.CodegenDsl with org.eclipse.xtext.xbase.Xbase
              
generate codegenDsl "http://www.thesoftwarefactory.com/vertx/web/mvc/CodegenDsl"

Model hidden(WS, ROUTE_COMMENT):
	routes+=Route*;

Route:
	verb=RouteVerb path=RoutePath handler=RouteHandler (permission=RoutePermission)? (role=RouteRole)? ';';

enum RouteVerb:
	GET='GET' |
	POST='POST' | 
	PUT='PUT' |
	DELETE='DELETE' | 
	HEAD='HEAD' |
	OPTIONS='OPTIONS';

RoutePath:
	{RoutePath}
	value=VALID_PATH;
	
RouteHandler:
	name=RouteHandlerName '(' (parameters+=Parameter (',' parameters+=Parameter)*)? ')';

Parameter:
	name=ID	(defaultValue=ParameterDefaultValue)?;

ParameterDefaultValue:
	'=' (new?='new' | null?='null' | value=STRING);

RouteHandlerName:
	ID ('.' ID)+;
	
RoutePermission:
	('PERMISSION' value=STRING);

RouteRole:
	('ROLE' value=STRING);

terminal VALID_PATH:
	'/' -> (' '|'\t'|'\r'|'\n');
	
terminal ROUTE_COMMENT:
	'#' !('\n'|'\r')* ('\r'? '\n')?;
